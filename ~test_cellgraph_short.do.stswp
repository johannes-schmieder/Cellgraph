// ================================================	
// ===== Code to test cellgraph.ado =====
// ================================================
clear all
set more off
set seed 12345
program drop _all
graph drop _all

do cellgraph.ado
 
set trace on
set tracedepth 1


// ====== Create Test Data ======

set obs 1000 
// Set up the base number of individuals and time period
local N = 1000
local tmin = 2000
local tmax = 2020

// Create an unbalanced panel by randomly dropping some observations
local rows = `N' * (`tmax' - `tmin' + 1)
set obs `rows'

// Generate person and year identifiers
gen person_id = ceil(_n/(`tmax' - `tmin' + 1))
bysort person_id: gen year = `tmin' + _n - 1

// Randomly drop some observations to create unbalanced panel (about 20%)
gen random = runiform()
drop if random < 0.2

// Generate time-invariant characteristics
by person_id: gen female = runiform() < 0.45 if _n == 1
by person_id: replace female = female[1] if _n > 1

// Generate age (starting between 25-45 in 2000)
by person_id: gen age = ceil(runiform() * 20 + 25) if year == 2000
by person_id: replace age = age[_n-1] + 1 if _n > 1

// Generate education (between 12-20 years, time-invariant)
by person_id: gen educ = ceil(runiform() * 8 + 12) if _n == 1
by person_id: replace educ = educ[1] if _n > 1

// Generate industry (1-5, allowing for some job changes)
by person_id: gen industry = ceil(runiform() * 5) if _n == 1
by person_id: replace industry = industry[_n-1] if _n > 1
replace industry = ceil(runiform() * 5) if runiform() < 0.1  // 10% chance of industry change

// Generate ESS (External Service Share) - time-invariant characteristic
// Finance (industry 5) has low ESS workers, creating omitted variable bias:
// wages increase in ESS within industry, but Finance has low ESS and high wages
by person_id: gen ess = runiform() * 0.5 if _n == 1
by person_id: replace ess = ess[1] if _n > 1
// Finance workers have lower ESS (beta distribution with low mean)
local mu = 0.05
local k = 1
local a = `mu' * `k'
local b = (1-`mu') * `k'
replace ess = rbeta(`a', `b') if industry == 5

// Generate log wage with returns to education, experience, gender gap, ESS, and industry premium
// Note: Finance (industry 5) has a large premium but low ESS workers - this creates OVB
gen experience = age - educ - 6
gen logwage = 1.5 + 0.08 * educ + 0.12 * experience - 0.002 * experience^2 ///
    - 0.15 * female + 0.05 * industry + 0.8 * ess + 0.5 * (industry == 5) ///
    + rnormal(0, 0.3) * (year-`tmin')*.03

	
g byear = year - age 

// Label variables
label var person_id "Person identifier"
label var year "Year"
label var age "Age in years"
label var educ "Years of education"
label var logwage "Log hourly wage"
label var female "Female (0/1)"
label var industry "Industry (1-5)"
label var experience "Years of potential experience"
label var byear "Birth Year"
label var ess "External Service Share"


// Label values for industry
label define ind_label 1 "Manufacturing" 2 "Services" 3 "Retail" 4 "Technology" 5 "Finance"
label values industry ind_label

label define female_label 0 "Male" 1 "Female"
label values female female_label

// Generate string variables for string by-var tests
gen str10 gender_str = "Male" if female == 0
replace gender_str = "Female" if female == 1
label var gender_str "Gender"
gen str20 industry_str = ""
replace industry_str = "Manufacturing" if industry == 1
replace industry_str = "Services" if industry == 2
replace industry_str = "Retail" if industry == 3
replace industry_str = "Technology" if industry == 4
replace industry_str = "Finance" if industry == 5
label var industry_str "Industry (1-5)"

// Sort dataset
sort person_id year


// ====== Test Cellgraph ======

local i 100 

g uniform = uniform()
replace age = . if uniform < .5
replace educ = . if uniform > .5
// cellgraph age educ, by(year) scatter coef lfit noci
 


// Omitted variable bias tests: wages increase in ESS within industry, 
// but Finance (industry 5) has low ESS and high wages
cellgraph logwage, by(ess industry) scatter coef lfit noci binscatter(20) name(g`i++')
cellgraph logwage, by(ess) scatter coef lfit noci binscatter(20) name(g`i++')
cellgraph logwage, by(ess) scatter coef lfit noci binscatter(20) controls(i.industry) name(g`i++')
cellgraph logwage, by(ess industry) scatter coef lfit noci binscatter(20) controls(i.industry) name(g`i++')


// Test string by-variables
cellgraph logwage, by(gender_str) noci name(g`i++')
cellgraph logwage, by(year gender_str) noci name(g`i++')
cellgraph logwage, by(industry_str female) noci name(g`i++')
cellgraph logwage, by(industry_str gender_str) noci name(g`i++')

exit 
cellgraph educ industry, by(year) 



cellgraph logwage , by(year female) name(g`i++') title("Log Wage") ///
	cipattern("shaded") lpattern ciopacity(10) nomsymbol

cellgraph logwage, by(year) name(g`i++') title("Log Wage") cipattern("lines") lpattern 



cellgraph logwage, by(year) name(g`i++') title("Log Wage") mcounts addnotes

cellgraph logwage, by(year female) name(g`i++') title("Log Wage") mcounts 

cellgraph logwage, by(year female) name(g`i++') title("Log Wage") mcounts colors(128 0 128; 128 128 0)

cellgraph logwage, by(year) name(g`i++') title("Log Wage") xline(0) stat(p10 p50 p90) ///
	legend(size(vsmall)) ylabel(,angle(horizontal)) ///
	yscale(range(2.7 4)) ylabel(3.5(.5)6) nomsymbol lpattern 


cellgraph logwage, by(year) name(g`i++') title("Log Wage") ///
	xline(0) mcounts stat(p10 p50 p90) legend(size(vsmall)) addnotes ylabel(,angle(horizontal)) ///
	nomsymbol lpattern gradient
	
cellgraph logwage, by(year) name(g`i++') title("Log Earnings") ///
	xline(0)   legend(size(small) pos(10) ring(0) col(1))  line  cipattern(shaded)



cellgraph logwage, by(year byear) gradient line  legend(off) noci

cellgraph logwage if age<=50 & byear>=1970, by(year byear) gradient line  legend(off) noci ///
	title(Wage - experience profiles for different birth cohorts)

